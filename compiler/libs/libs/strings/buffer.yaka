@nativedefine("yk__sds*")
class StringBuffer:
    # String buffer object
    pass

@native
def new() -> StringBuffer:
    # Create a new empty string buffer
    ccode """yk__sds* srr = calloc(1, sizeof(yk__sds*));
    srr[0] = yk__sdsempty();
    return srr"""

@native
def new_size(s: int) -> StringBuffer:
    # Create a string buffer with capacity s
    ccode """yk__sds* srr = calloc(1, sizeof(yk__sds*));
    srr[0] = yk__sdsempty();
    srr[0] = yk__sdsgrowzero(srr[0], nn__s);
    return srr"""

@native
def append(buf: StringBuffer, v: sr) -> None:
    # Append a string to string buffer
    ccode """nn__buf[0] = yk__sdscatlen(nn__buf[0], yk__bstr_get_reference(nn__v), yk__bstr_len(nn__v))"""

@native
def prepend(buf: StringBuffer, v: sr) -> None:
    # Prepend a string to string buffer
    ccode """yk__sds tmp = nn__buf[0];
    nn__buf[0] = yk__sdscatsds(yk__bstr_copy_to_sds(nn__v), nn__buf[0]);
    yk__sdsfree(tmp);"""

@native
def to_str(buf: StringBuffer) -> str:
    # Get a new string copy out of string buffer
    ccode """return yk__sdsdup(nn__buf[0])"""

@native
def to_sr(buf: StringBuffer) -> sr:
    # Get a string reference out of string buffer
    ccode """return yk__bstr_h(nn__buf[0])"""

@native
def join(buf: StringBuffer, values: Array[str], sep: sr) -> None:
    # Join given values in array with provided seperator and append this to string
    ccode """if (NULL == nn__values) {
        return;
    }
    yk__sds tmp = yk__sdsjoinsds(nn__values, yk__arrlen(nn__values), yk__bstr_get_reference(nn__sep), yk__bstr_len(nn__sep));
    nn__buf[0] = yk__sdscatsds(nn__buf[0], tmp);
    yk__sdsfree(tmp)"""

def join_sr(buf: StringBuffer, values: Array[sr], sep: sr) -> None:
    # Join given values in array with provided seperator and append this to string buffer
    length = len(values)
    if (length == 0):
        return
    for (i = 0; i < length; i = i + 1):
        append(buf, values[i])
        if (i < length - 1):
            append(buf, sep)

@native
def del_buf(buf: StringBuffer) -> None:
    # Delete string buffer object
    ccode """yk__sdsfree(nn__buf[0]); 
    free(nn__buf)"""
